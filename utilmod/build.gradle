plugins {
    id 'net.neoforged.moddev'
}

group = 'cpw.mods.forge.serverpackutility'
version = rootProject.version
archivesBaseName = rootProject.name + "-" + project.name

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
}

neoForge {
    version = project.neoforgeVersion

    mods {
        utilmod {
            modSourceSets = [sourceSets.main]
        }
    }

    runs {
        client {
            client()
            gameDirectory = file("run/client")
        }
        server {
            server()
            gameDirectory = file("run/server")
        }
    }
}

runClient {
    args("--launch_target", mainClass.get())
    mainClass = "net.covers1624.devlogin.DevLogin"
}

dependencies {
    clientAdditionalRuntimeClasspath project(path: ":", configuration: 'shadowedLibraryElements')
    serverAdditionalRuntimeClasspath project(path: ":", configuration: 'shadowedLibraryElements')
    implementation project(':')
    runtimeOnly "net.covers1624:DevLogin:0.1.0.4"
}
