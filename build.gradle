plugins {
    id 'org.ajoberstar.reckon' version '0.11.0'
    id 'org.ajoberstar.grgit' version '3.1.1'
    id 'com.github.ben-manes.versions' version '0.22.0'
}

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'java-library'

group 'cpw.mods.forge'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        name = "forge"
        url = "http://files.minecraftforge.net/maven"
    }
}

jacoco {
    toolVersion = "0.8.2"
}

reckon {
    scopeFromProp()
    stageFromProp('ms', 'final')
}

reckonTagCreate.dependsOn check

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

test {
    useJUnitPlatform()
}

ext.sharedManifest = manifest {
    attributes(
            "Class-Path": [
                    "forge-1.14.4-28.1.6.jar",
                    "libraries/org/ow2/asm/asm/6.2/asm-6.2.jar",
                    "libraries/org/ow2/asm/asm-commons/6.2/asm-commons-6.2.jar",
                    "libraries/org/ow2/asm/asm-tree/6.2/asm-tree-6.2.jar",
                    "libraries/cpw/mods/modlauncher/3.2.0/modlauncher-3.2.0.jar",
                    "libraries/cpw/mods/grossjava9hacks/1.1.0/grossjava9hacks-1.1.0.jar",
                    "libraries/net/minecraftforge/accesstransformers/1.0.0-shadowed/accesstransformers-1.0.0-shadowed.jar",
                    "libraries/net/minecraftforge/eventbus/1.0.0-service/eventbus-1.0.0-service.jar",
                    "libraries/net/minecraftforge/forgespi/1.3.0/forgespi-1.3.0.jar",
                    "libraries/net/minecraftforge/coremods/1.0.0/coremods-1.0.0.jar",
                    "libraries/net/minecraftforge/unsafe/0.2.0/unsafe-0.2.0.jar",
                    "libraries/com/electronwill/night-config/core/3.6.0/core-3.6.0.jar",
                    "libraries/com/electronwill/night-config/toml/3.6.0/toml-3.6.0.jar",
                    "libraries/org/jline/jline/3.12.1/jline-3.12.1.jar",
                    "libraries/org/apache/maven/maven-artifact/3.6.0/maven-artifact-3.6.0.jar",
                    "libraries/net/jodah/typetools/0.6.0/typetools-0.6.0.jar",
                    "libraries/java3d/vecmath/1.5.2/vecmath-1.5.2.jar",
                    "libraries/org/apache/logging/log4j/log4j-api/2.11.2/log4j-api-2.11.2.jar",
                    "libraries/org/apache/logging/log4j/log4j-core/2.11.2/log4j-core-2.11.2.jar",
                    "libraries/net/minecrell/terminalconsoleappender/1.2.0/terminalconsoleappender-1.2.0.jar",
                    "libraries/net/sf/jopt-simple/jopt-simple/5.0.4/jopt-simple-5.0.4.jar",
                    "libraries/net/minecraft/server/1.14.4/server-1.14.4-extra.jar"
            ].join(' '),
            'Main-Class': 'net.minecraftforge.server.ServerMain',
            'ServerLaunchArgs': '--gameDir . --launchTarget fmlserver --fml.forgeVersion 28.1.6 --fml.mcpVersion 20190829.143755 --fml.mcVersion 1.14.4 --fml.forgeGroup net.minecraftforge --assets .cache'
    )
    attributes(
            ["Specification-Title"     : "serverpacklocator",
             "Specification-Vendor"    : "cpw",
             "Specification-Version"   : "1", // Currently version 3 of the forgespi specification
             "Implementation-Title"    : project.name,
             "Implementation-Version"  : "${version}+${System.getenv("BUILD_NUMBER") ?: 0}+${grgit.head().abbreviatedId}",
             "Implementation-Vendor"   : "cpw",
             "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
             "Git-Commit"              : grgit.head().abbreviatedId,
             "Git-Branch"              : grgit.branch.current().getName()],
            "cpw/mods/forge/cursepacklocator/")
}

jar {
    manifest = project.manifest {
        from sharedManifest
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}


dependencies {
    implementation("net.minecraftforge:forgespi:1.3.+")
    implementation("cpw.mods:modlauncher:3.2.+")
    implementation("com.google.code.gson:gson:2.8.0")
    implementation("org.apache.logging.log4j:log4j-api:2.11.2")
    implementation("io.netty:netty-all:4.1.25.Final")
}

task zip(type: Zip, dependsOn: jar) {
    archiveClassifier = "bundle"
    from (jar.outputs) { into ("libraries/cpw/mods/forge/serverpacklocator/${project.version}/") }
    from (file("1.14.4-serverpacklocator.json")) {
        filter { line -> line.replace('@version@', "${project.version}") }
        into ("versions/1.14.4-serverpacklocator/")
    }
}

artifacts {
    archives zip
    archives jar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact zip
            pom {
                name = 'Server Pack Mod Locator'
                description = 'Server Pack Mod Locator'
                url = 'https://github.com/cpw/serverpackmodlocator'
                scm {
                    url = 'https://github.com/cpw/serverpackmodlocator'
                    connection = 'scm:git:git://github.com/cpw/serverpackmodlocator.git'
                    developerConnection = 'scm:git:git@github.com:cpw/serverpackmodlocator.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/cpw/serverpackmodlocator/issues'
                }

                licenses {
                    license {
                        name = 'LGPLv2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }

                developers {
                    developer {
                        id = 'cpw'
                        name = 'cpw'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username project.properties.cpwMavenUser?:'fake'
                password project.properties.cpwMavenPassword?:'news'
            }
            url "http://files.minecraftforge.net/maven/manage/upload"
        }
    }
}