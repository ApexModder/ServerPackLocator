
plugins {
    id 'maven-publish'
    id 'net.neoforged.gradleutils'
    id 'net.neoforged.moddev'
    id 'io.github.goooler.shadow'
}

gradleutils {
    version {
        branches {
            suffixBranch()
            suffixExemptedBranch('main')
        }
    }
}

version = gradleutils.version.toString()

group = 'cpw.mods.forge'

neoForge {
    version = project.neoforgeVersion

    mods {
        utilmod {
            modSourceSets = [sourceSets.main]
        }
    }

    runs {
        client {
            client()
            gameDirectory = file("runs/client")
            programArgument "--launch_target"
            programArgument "cpw.mods.bootstraplauncher.BootstrapLauncher"
            mainClass = "net.covers1624.devlogin.DevLogin"
        }
        server {
            server()
            gameDirectory = file("runs/server")
        }
    }
}

configurations {
    utilMod {
        canBeResolved = true
        canBeConsumed = false
        transitive = false
        withDependencies {
            it.add(dependencyFactory.create(project(":utilmod")))
        }
    }
    shadowedLibraryElements {
        canBeConsumed = true
        extendsFrom shadow
    }
    clientAdditionalRuntimeClasspath.extendsFrom shadow
    serverAdditionalRuntimeClasspath.extendsFrom shadow
}

def copyUtilMod = tasks.register("copyUtilMod", Copy) {
    from(configurations.utilMod)
    into(layout.buildDirectory.dir("generated/splUtilMod"))
    rename {
        // The shadow plugin says we should rename this to not have a .jar ending
        return "SPL-utilmod.embedded"
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
}

jar.configure {
    archiveClassifier = "plain"
    manifest {
        attributes(
                'Automatic-Module-Name': 'serverpacklocator'
        )
    }
}

shadowJar {
    archiveClassifier = ""
    from(copyUtilMod)
    configurations = [project.configurations.shadow]
    relocate 'io.netty.handler.codec.http', 'shadow.io.netty.handler.codec.http'
    relocate 'io.netty.handler.codec.rtsp', 'shadow.io.netty.handler.codec.rtsp'
    relocate 'io.netty.handler.codec.spdy', 'shadow.io.netty.handler.codec.spdy'
    relocate 'com.electronwill.nightconfig.json', 'shadow.com.electronwill.nightconfig.json'
}

dependencies {
    shadow("io.netty:netty-codec-http:4.1.97.Final") {
        transitive = false
    }
    shadow("com.electronwill.night-config:json:3.6.4") {
        transitive = false
    }
    runtimeOnly "net.covers1624:DevLogin:0.1.0.4"
}

assemble.dependsOn shadowJar
