
plugins {
    id 'maven-publish'
    id 'java-library'
    id 'net.neoforged.gradleutils'
    id 'io.github.goooler.shadow'
}

gradleutils {
    version {
        branches {
            suffixBranch()
            suffixExemptedBranch('main')
        }
    }
}

version = gradleutils.version.toString()

group = 'cpw.mods.forge'

configurations {
    utilMod {
        canBeResolved = true
        canBeConsumed = false
        transitive = false
        withDependencies {
            it.add(dependencyFactory.create(project(":utilmod")))
        }
    }
    shadowedLibraryElements {
        canBeConsumed = true
        extendsFrom shadow
    }
}

def copyUtilMod = tasks.register("copyUtilMod", Copy) {
    from(configurations.utilMod)
    into(layout.buildDirectory.dir("generated/splUtilMod"))
    rename {
        // The shadow plugin says we should rename this to not have a .jar ending
        return "SPL-utilmod.embedded"
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
}

jar.configure {
    manifest {
        attributes(
                'Automatic-Module-Name': 'serverpacklocator'
        )
    }
}

shadowJar {
    from(copyUtilMod)
    configurations = [project.configurations.shadow]
    relocate 'io.netty.handler.codec.http', 'shadow.io.netty.handler.codec.http'
    relocate 'io.netty.handler.codec.rtsp', 'shadow.io.netty.handler.codec.rtsp'
    relocate 'io.netty.handler.codec.spdy', 'shadow.io.netty.handler.codec.spdy'
    relocate 'com.electronwill.nightconfig.json', 'shadow.com.electronwill.nightconfig.json'
}

dependencies {
    implementation("net.neoforged.fancymodloader:loader:4.0.11")

    implementation("com.google.code.gson:gson:2.10.1")
    implementation("com.mojang:authlib:6.0.52")
    implementation("org.apache.logging.log4j:log4j-api:2.19.0")
    implementation("io.netty:netty-codec-http:4.1.97.Final")
    shadow("io.netty:netty-codec-http:4.1.97.Final") {
        transitive = false
    }
    implementation("com.electronwill.night-config:core:3.6.4")
    implementation("com.electronwill.night-config:toml:3.6.4")
    shadow("com.electronwill.night-config:json:3.6.4") {
        transitive = false
    }
    implementation('org.apache.maven:maven-artifact:3.9.6')
}

assemble.dependsOn shadowJar
